
const HDLD_GRE_GAS="tgg";
const ENC_GASGRENADE=18;

class HDGasGrenades:HDGrenadethrower{
	default{
		weapon.selectionorder 1020;
		weapon.slotpriority 1;
		weapon.slotnumber 0;
		tag "Toxic Gas Grenades";
		hdgrenadethrower.ammotype "HDGasGrenadeAmmo";
		hdgrenadethrower.throwtype "HDGasGrenadeThrown";
		hdgrenadethrower.spoontype "HDGasGrenadeSpoon";
		hdgrenadethrower.wiretype "GasTripwireFrag";
		inventory.icon "GGR2A0";
	}

	override string gethelptext(){
		if(weaponstatus[0]&FRAGF_SPOONOFF)return
		WEPHELP_FIRE.."  Wind up, release to throw\n(\cxSTOP READING AND DO THIS"..WEPHELP_RGCOL..")";
		return
		WEPHELP_FIRE.."  Pull pin/wind up (release to throw)\n"
		..WEPHELP_ALTFIRE.."  Pull pin, again to drop spoon\n"
		..WEPHELP_RELOAD.."  Abort/replace pin\n"
		..WEPHELP_ZOOM.."  Start planting tripwire traps"
		;
	}
	
	override string,double getpickupsprite(){return "GGR2A0",0.7;}
	override void DrawHUDStuff(HDStatusBar sb,HDWeapon hdw,HDPlayerPawn hpl){
		if(sb.hudlevel==1){
			sb.drawimage(
				(weaponstatus[0]&FRAGF_PINOUT)?"GGR2B0":"GGR2A0",
				(-52,-4),sb.DI_SCREEN_CENTER_BOTTOM,scale:(0.7,0.7)
			);
			sb.drawnum(hpl.countinv("HDGasGrenadeAmmo"),-45,-8,sb.DI_SCREEN_CENTER_BOTTOM);
		}
		sb.drawwepnum(
			hpl.countinv("HDGasGrenadeAmmo"),
			(HDCONST_MAXPOCKETSPACE/ENC_FRAG)
		);
		sb.drawwepnum(hdw.weaponstatus[FRAGS_FORCE],50,posy:-10,alwaysprecise:true);
		if(!(hdw.weaponstatus[0]&FRAGF_SPOONOFF)){
			sb.drawrect(-21,-19,5,4);
			if(!(hdw.weaponstatus[0]&FRAGF_PINOUT))sb.drawrect(-25,-18,3,2);
		}else{
			int timer=hdw.weaponstatus[FRAGS_TIMER];
			if(timer%3)sb.drawwepnum(140-timer,140,posy:-15,alwaysprecise:true);
		}
	}
	
	override void ForceBasicAmmo(){
		owner.A_SetInventory("HDGasGrenadeAmmo",1);
	}
	
	states
	{
		deselectinstant:
			TNT1 A -1 A_TakeInventory("HDGasGrenades", 1);
			stop;
	}
}

class GasTripwireFrag:Tripwire{
	default{
		weapon.selectionorder 1021;
		tripwire.ammotype "HDGasGrenadeAmmo";
		tripwire.throwtype "GasTrippingFrag";
		tripwire.spoontype "HDGasSpoon";
		tripwire.weptype "HDGasGrenades";
	}
	override void DrawHUDStuff(HDStatusBar sb,HDWeapon hdw,HDPlayerPawn hpl){
		if(sb.hudlevel==1){
			sb.drawimage("GGR2A0",(-52,-4),sb.DI_SCREEN_CENTER_BOTTOM,scale:(0.7,0.7));
			sb.drawnum(hpl.countinv("HDGasGrenadeAmmo"),-45,-8,sb.DI_SCREEN_CENTER_BOTTOM);
		}
		sb.drawwepnum(
			hpl.countinv("HDGasGrenadeAmmo"),
			(ENC_FRAG/HDCONST_MAXPOCKETSPACE)
		);
		sb.drawwepnum(hdw.weaponstatus[FRAGS_FORCE],50,posy:-10,alwaysprecise:true);
		if(!(hdw.weaponstatus[0]&FRAGF_SPOONOFF)){
			sb.drawrect(-21,-19,5,4);
			if(!(hdw.weaponstatus[0]&FRAGF_PINOUT))sb.drawrect(-25,-18,3,2);
		}else{
			int timer=hdw.weaponstatus[FRAGS_TIMER];
			if(timer%3)sb.drawwepnum(140-timer,140,posy:-15,alwaysprecise:true);
		}
	}
}

class HDGasGrenade:HDFragGrenade{
	default{
		+ROLLSPRITE
		+ROLLCENTER
		scale 0.4;
		obituary "%o was smoked by %k.";
		hdfraggrenade.rollertype "HDGasGrenadeRoller";
	}
	states
	{
	spawn:
		GGR3 ABCDEFGH 2 A_SetRoll(frandom(-15.5, 15.5));
		loop;
	}
}

class GasTrippingFrag:TrippingGrenade{
	default{
		//$Category "Misc/Hideous Destructor/Traps"
		//$Title "Tripwire Grenade"
		//$Sprite "FRAGA0"

		scale 0.3;
		trippinggrenade.rollertype "HDGasGrenadeRoller";
		trippinggrenade.spoontype "HDGasSpoon";
		trippinggrenade.droptype "HDGasGrenadeAmmo";
		hdupk.pickuptype "HDGasGrenadeAmmo";
	}
	override void postbeginplay(){
		super.postbeginplay();
		pickupmessage=getdefaultbytype("HDGasGrenadeAmmo").pickupmessage();
	}
	states{
	spawn:
		GGR3 A 1 nodelay A_TrackStuckHeight();
		wait;
	}
}

class HDGasGrenadeAmmo:HDAmmo{
	default{
		//+forcexybillboard
		inventory.icon "GGR2A0";
		inventory.amount 1;
		scale 0.3;
		inventory.maxamount 50;
		inventory.pickupmessage "Picked up a Toxic Gas Grenade.";
		inventory.pickupsound "weapons/pocket";
		tag "Toxic Gas Grenades";
		hdpickup.refid HDLD_GRE_GAS;
		hdpickup.bulk ENC_GASGRENADE;
		+INVENTORY.KEEPDEPLETED
	}
	override bool IsUsed(){return true;}
	override void AttachToOwner(Actor user)
	{
		user.GiveInventory("HDGasGrenades", 1);
		super.AttachToOwner(user);
	}
	override void DetachFromOwner()
	{
		if(!(owner.player.ReadyWeapon is "HDGasGrenades"))
		{
			TakeInventory("HDGasGrenades", 1);
		}
		super.DetachFromOwner();
	}
	states{
	spawn:
		GGR2 A -1;stop;
	}
}

class HDGasSpoon : HDFragSpoon
{
	default
	{
		Scale 0.3;bouncefactor 0.8;
		bouncesound "misc/casing4";
	}
	states{
	spawn:
		TNT1 A 0;
		stop;
		GGR2 C 2{roll+=40;}wait;
	death:
		GGR2 C -1;
	}
}

//Big thanks to Agent_Ash/Jekyll Grim Payne, and Phantombeta for helping me out
// because I'm a doo doo head when it comes to coding anything

//GAS

class HD_PBGasEmit:HDPuff{
	default{
		obituary "%o smelled something really bad.";
		scale 1.25;gravity 0.05;alpha 0.7;
		hdpuff.fadeafter 12000;
		hdpuff.decel 0.96;
		hdpuff.fade 0.96;
		hdpuff.grow 0.02;
		hdpuff.minalpha 0.005;
		BounceCount 999;
		-NOCLIP
		+THRUACTORS
		+BOUNCEONFLOORS +BOUNCEONCEILINGS +BOUNCEONWALLS
		//renderstyle "add";
	}
	
	// For the UAS resperator support.
	service RespStatus;
	bool IsPlayerUsingRespirator(HDPlayerPawn ppp){
		// prematurely returns if ppp is not a player pointer, 
		// or if the service is not available. 
		if(!ppp || !respstatus)return false;
		// returns the status of the resperator. 
		return int(RespStatus.GetInt("IsWorn", objectArg:ppp));
	}
	override void postbeginplay(){
		HDActor.postbeginplay();
		if(max(abs(pos.x),abs(pos.y),abs(pos.z))>=32768){destroy();return;}
		grow*=scale.x;
		vel.z+=startvelz;
		grav=getgravity();

		
		// Looks for a pointer to respstatus,
		// this does nothing if respstatus doesn't exist. 
		ServiceIterator i = ServiceIterator.Find("UaS_RespiratorStatus");
		while((respstatus = i.next()) && !respstatus){};
		
	}
	
	action void A_PoisonAoE (double aoeRadius, int damage, int duration
	, int period, name damageType) {
            BlockThingsIterator itr = BlockThingsIterator.Create(self,128);
            while (itr.next()) {
                let next = itr.thing;
                if (next.bKILLED)
                    continue;
				if (next.countinv("WornRadsuit"))
                    continue; 
					
				// here we check if the player is wearing a resperator. 
				if(invoker.IsPlayerUsingRespirator(HDPlayerPawn(next)))
					continue;
				if (next.bNOBLOOD)
                    continue; 
                double dist = Distance3D(next);
                if (dist > aoeRadius)
                    continue;
				if (!CheckSight (next, SF_IGNOREVISIBILITY | SF_IGNOREWATERBOUNDARY))
                continue;
                next.PoisonMobj(self, target, damage, duration, period, damageType);
		}
	}
	states{
	spawn:
		GAS1 AABB 1 
				A_ChangeVelocity(
					frandom(-0.1,0.1),frandom(-0.1,0.1),
					0.55,
					CVF_RELATIVE
				);
		GAS1 CDEEEFFFGGGHHH 1 {
		A_PoisonAoE(32,30,20,0,"slime");
		A_ChangeVelocity(
					frandom(-1,1),frandom(-1,1),
					0.15,
					CVF_RELATIVE
				);}
	hurtloop2:
		GAS1 FFFFFFFFGGGGGGGGHHHHHHHHIIIIIIIIJJJJJJJJ 1 
		{A_PoisonAoE(128,12,10,0,"slime");
				A_ChangeVelocity(
					frandom(-1.5,1.5),frandom(-1.5,1.5),
					frandom(-1.5,1.5),
					CVF_RELATIVE
				);}
		loop;
	}
}

class HD_PBGasEmit2:HD_PBGasEmit{
	default{
		obituary "%o smelled something really bad.";
		scale 2.25;gravity 0.05;alpha 0.7;
		hdpuff.fadeafter 900;
		hdpuff.decel 0.96;
		hdpuff.fade 0.96;
		hdpuff.grow 0.01;
		hdpuff.minalpha 0.005;
	}
	states{
	spawn:
		GAS2 AAAAABBBBBCCCCCDDDDDEEEEEFFFFFGGGGGHHHHHIIIIIJJJJJKKKKKLLLLLMMMMMNNNNNOOOOOOPPPPPQQQQQRRRRRSSSSSTTTTT 1 A_PoisonAoE(64,1,2,0,"hot");
	}
}

//GRENADE

class HDGasGrenadeRoller:HDActor{
	int fuze;
	int gasamount;
	vector3 keeprolling;
	default{
		-noextremedeath -floorclip +shootable +noblood +forcexybillboard
		+activatemcross -noteleport +noblockmonst
		+missile +bounceonactors +usebouncestate 
			bouncetype "doom";bouncesound "misc/fragknock";
		radius 2;height 2;damagetype "none";
		scale 0.4;
		obituary "%o was fragged by %k.";
		radiusdamagefactor 0.04;pushfactor 1.4;maxstepheight 2;mass 30;
	}
	
/*	override bool used(actor user){
		angle=user.angle;
		A_StartSound(bouncesound);
		if(hdplayerpawn(user)&&hdplayerpawn(user).incapacitated)A_ChangeVelocity(4,0,1,CVF_RELATIVE);
		else A_ChangeVelocity(12,0,4,CVF_RELATIVE);
		return true;
		}
*/
	states{
	
	spawn:
		GGR1 A 0 nodelay{
			HDMobAI.Frighten(self,512);
		}
	spawn2:
		#### AAA 2{
			if(abs(vel.z-keeprolling.z)>10)A_StartSound("misc/fragknock",CHAN_BODY);
			else if(floorz>=pos.z)A_StartSound("misc/fragroll");
			keeprolling=vel;
			if(abs(vel.x)<0.4 && abs(vel.y)<0.4) setstatelabel("death");
		}loop;
	bounce:
		---- A 0{
			bmissile=false;
			vel*=0.3;
		}goto spawn2;
	death:
		---- A 2{
			if(abs(vel.z-keeprolling.z)>3){
				A_StartSound("misc/fragknock",CHAN_BODY);
				keeprolling=vel;
			}
			if(abs(vel.x)>0.4 || abs(vel.y)>0.4) setstatelabel("spawn");
		}wait;
		
	nomore:
		---- A 0 A_StopSound(CHAN_WEAPON);
		/*---- A 1 {
		bsolid=false;bpushable=false;bmissile=false;bnointeraction=true;bshootable=false;
		}*/
		---- A 1 A_Fadeout(0.01);
		loop;
	}
	override void tick(){
	
		if(fuze>140){
			keeprolling=vel;
			super.tick();
		}

		if(isfrozen())return;
		else if(bnointeraction){
			//nexttic
			if(CheckNoDelay()){
				if(tics>0)tics--;  
				while(!tics){
					if(!SetState(CurState.NextState)){
						return;
					}
				}
			}
			return;
		}else{
			fuze++;
			if(fuze>=140){
				gasamount++;
				
			if(gasamount>=1&&gasamount<=801){actor xpl=spawn("HD_PBGasEmit",self.pos-(frandom(-4,4),0,frandom(-4,4)));
				A_StartSound("weapons/grenade/gas/end",CHAN_BODY,CHANF_OVERLAP,0.1,10);}
				
				if(CheckNoDelay()){
					if(tics>0)tics--;  
					while(!tics){
						if(!SetState(CurState.NextState)){
							return;
						}
					}
				}
				return;
			}else super.tick();
		}
	}
}

class HDGasGrenadeThrown:HDFragGrenade{
	default{
		+ROLLSPRITE
		+ROLLCENTER
		scale 0.4;
		obituary "%o smelled something really bad.";
		hdfraggrenade.rollertype "HDGasGrenadeRoller";
	}
	states
	{
	spawn:
		GGR3 BCDE 2 A_SetRoll(frandom(-15.5, 15.5));
		loop;
		
	death:
		TNT1 A 10{
			bmissile=false;
			let gr=HDGasGrenadeRoller(spawn(rollertype,self.pos,ALLOW_REPLACE));
			if(!gr)return;
			gr.target=self.target;gr.master=self.master;
			gr.fuze=self.fuze;
			gr.vel=self.keeprolling;
			gr.keeprolling=self.keeprolling;
			gr.A_StartSound("misc/fragknock",CHAN_BODY);
			HDMobAI.Frighten(gr,512);
		}stop;
	}
}
class HDGasGrenadeSpoon:HDDebris{
	default{
		scale 0.3;bouncefactor 0.8;
		bouncesound "misc/casing4";
	}
	override void postbeginplay(){
		super.postbeginplay();
		A_StartSound("weapons/grenopen",CHAN_VOICE);
	}
	states{
	spawn:
		GGR2 C 2{roll+=40;}wait;
	death:
		GGR2 C -1;
	}
}